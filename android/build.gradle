buildscript {
  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:8.1.0'
  }
}

apply plugin: 'com.android.library'

def safeExtGet(String prop, String fallback) {
  rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

// Path to local FFmpeg AAR files (relative to this module's directory)
def ffmpegLibsDir = new File(projectDir, '../../ffmpeg/android')

android {
  if (project.android.hasProperty("namespace")) {
    namespace 'com.arthenica.ffmpegkit.reactnative'
  }

  compileSdkVersion 33

  defaultConfig {
    minSdkVersion 24
    targetSdkVersion 33
    versionCode 602
    versionName "6.0.2"
  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }
  lintOptions {
    disable 'GradleCompatible'
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_11
    targetCompatibility JavaVersion.VERSION_11
  }

  // Packaging options to handle duplicate .so files from AAR
  packagingOptions {
    pickFirst 'lib/*/libffmpegkit.so'
    pickFirst 'lib/*/libavcodec.so'
    pickFirst 'lib/*/libavformat.so'
    pickFirst 'lib/*/libavutil.so'
    pickFirst 'lib/*/libavfilter.so'
    pickFirst 'lib/*/libavdevice.so'
    pickFirst 'lib/*/libswresample.so'
    pickFirst 'lib/*/libswscale.so'
  }
}

repositories {
  google()
  mavenCentral()

  // Local FFmpeg AAR files from consuming app's ffmpeg/android/ directory
  flatDir {
    dirs ffmpegLibsDir
  }

  def found = false
  def defaultDir = null
  def androidSourcesName = 'React Native sources'

  if (rootProject.ext.has('reactNativeAndroidRoot')) {
    defaultDir = rootProject.ext.get('reactNativeAndroidRoot')
  } else {
    defaultDir = new File(
      projectDir,
      'node_modules/react-native/android'
    )
  }

  if (defaultDir.exists()) {
    maven {
      url defaultDir.toString()
      name androidSourcesName
    }

    logger.info(":${project.name}:reactNativeAndroidRoot ${defaultDir.canonicalPath}")
    found = true
  } else {
    def parentDir = rootProject.projectDir

    1.upto(5, {
      if (found) return true
      parentDir = parentDir.parentFile

      def androidSourcesDir = new File(
        parentDir,
        'node_modules/react-native'
      )

      def androidPrebuiltBinaryDir = new File(
        parentDir,
        'node_modules/react-native/android'
      )

      if (androidPrebuiltBinaryDir.exists()) {
        maven {
          url androidPrebuiltBinaryDir.toString()
          name androidSourcesName
        }

        logger.info(":${project.name}:reactNativeAndroidRoot ${androidPrebuiltBinaryDir.canonicalPath}")
        found = true
      } else if (androidSourcesDir.exists()) {
        maven {
          url androidSourcesDir.toString()
          name androidSourcesName
        }

        logger.info(":${project.name}:reactNativeAndroidRoot ${androidSourcesDir.canonicalPath}")
        found = true
      }
    })
  }

  if (!found) {
    throw new GradleException(
      "${project.name}: unable to locate React Native android sources. " +
        "Ensure you have you installed React Native as a dependency in your project and try again."
    )
  }
}

dependencies {
  api 'com.facebook.react:react-native:+'

  // Local FFmpeg AAR files - include all AAR files from ffmpeg/android/
  implementation fileTree(dir: ffmpegLibsDir, include: ['*.aar'])
}
